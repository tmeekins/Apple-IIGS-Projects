**************************************************************************
*                                                                        *
* Common macros used by most other macros and in applications            *
* Written by Tim Meekins                                                 *
* November 5, 1988                                                       *
*                                                                        *
* v1.0  11/05/88 Original version                                        *
*        9/08/89 Wrote Pull macros, PL2, PL4.                            *
*        9/12/89 Wrote ProDOS 16 dispatcher.                             *
*       10/28/89 Wrote GS/OS dispatcher, Str, Str1, and GSStr macro.     *
*       12/12/89 Wrote LD2, LD4, MV2, MV4.                               *
*       12/13/89 Wrote proc, procend, procendL.                          *
*       12/14/89 Bug fixes in proc. Wrote bgt, ble.                      *
*       12/15/89 Wrote long, short.                                      *
*       12/16/89 Wrote cm2, lsr2, lsr3, lsr4, asl2, asl3, asl4,          *
*                inx2,inx4,iny2,iny4,inc2,inc4,dex2,dex4,dey2,dey4,      *
*                dec2,dec4                                               *
*        1/05/90 Wrote VBLWait                                           *
*        1/06/90 Fixed bug in ld4                                        *
*        1/10/90 Wrote Add2,Sub2.                                        *
*        3/11/90 Wrote Add4                                              *
*        3/13/90 Add4 now works with immediate mode.                     *
*                Wrote Get2,Get4,Sto2,Sto4.                              *
*        3/28/90 Wrote Sub4                                              *
*        6/30/90 Wrote SAdd2,SSub2,P8,native,emulate.                    *
*        7/13/90 Added @X,@Y to add2.                                    *
*        7/14/90 Wrote and2, ora2, and eor2.                             *
*        8/01/90 Added @X,@Y to sub2.                                    *
*        8/11/90 Added parameter addrs to LD2,LD4,MV2, & MV4             *
* v1.1   8/23/90 Fixed MAJOR typo/bug in LD4                             *
*        8/30/90 Emulate now sets short registers                        *
*        8/31/90 MAJOR BUG fix. ProDOS 8 used a JSL!!                    *
*                Wrote Smartport, SmartportX                             *
*        9/19/90 Added @xy etc.. to PL4.                                 *
*       10/17/90 Added '@' support to PH4                                *
*       12/05/90 Wrote Neg2                                              *
*       12/06/90 Wrote IF2                                               *
*       12/15/90 Removed CM2                                             *
* v1.2  12/20/90 Wrote a really simple Deref macro                       *
* v1.3  04/27/91 Wrote UserTool                                          *
* v1.4  05/24/91 Wrote JEQ,JNE,JPL,JMI,JCC,JCS,JGE,JLT.                  *
*                                                                        *
**************************************************************************
*                                                                        *
*                    ZAVTRA MACRO UTILITIES DISK                         *
*                   Copyright 1990 by Tim Meekins                        *
*                        All Rights Reserved                             *
*                                                                        *
*                   THIS PRODUCT IS SHAREWARE!!                          *
*   If you find yourself using this product extensively, please take the *
*   time and send several dollars to the author to compensate for the    *
*   many hours spent developing this product for your use. Support of    *
*   this product will help in the development of future products.        *
*                                                                        *
**************************************************************************

               macro
&lab           tool  &a1
&lab           ldx   #&a1
               jsl   $e10000
               mend

*************************************

               macro
&lab           usertool  &a1
&lab           ldx   #&a1
               jsl   $e10008
               mend

*************************************

               macro
&lab           p8    &a1,&a2
&lab           jsr   $BF00
               dc    i1'&a1'
               dc    i2'&a2'
               mend

*************************************

               macro
&lab           p16   &a1,&a2
&lab           jsl   $E100A8
               dc    i2'&a1'
               dc    i4'&a2'
               mend

*************************************

               macro
&lab           gsos  &a1,&a2
&lab           jsl   $E100A8
               dc    i2'&a1'
               dc    i4'&a2'
               mend

*************************************

               macro
&lab           smartport &a1,&a2
&lab           jsr   $C50D
               dc    i1'&a1'
               dc    i2'&a2'
               mend

*************************************

               macro
&lab           smartportX &a1,&a2
&lab           jsr   $C50D
               dc    i1'&a1'
               dc    i4'&a2'
               mend

*************************************

               macro
&lab           ph2   &parm
               lclc  &char
&lab           anop
               aif   c:&parm=0,.done
&char          amid  &parm,1,1
               aif   "&char"="#",.immediate
               aif   "&char"="@",.at
               aif   s:longa=1,.chk
               rep   #%00100000
.chk
               aif   "&char"<>"{",.absolute
&char          amid  &parm,l:&parm,1
               aif   "&char"<>"}",.error
&parm          amid  &parm,2,l:&parm-2
               lda   (&parm)
               pha
               ago   .shorten
.absolute
               lda   &parm
               pha
               ago   .shorten
.immediate
&parm          amid  &parm,2,l:&parm-1
               pea   &parm
               ago   .done
.at
&char          amid  &parm,2,1
               ph&char
.shorten
               aif   s:longa=1,.done
               sep   #%00100000
.done
               mexit
.error
               mnote "Missing closing '}'",16
               mend

*************************************************

               macro
&lab           ph4   &parm
               lclc  &char
               lclc  &char1
               lclc  &char2
&lab           anop
&char          amid  &parm,1,1
               aif   "&char"="#",.immediate
               aif   "&char"="@",.at
               aif   s:longa=1,.chk1
               rep   #%00100000
.chk1
               aif   "&char"<>"{",.chk2
&char          amid  &parm,l:&parm,1
               aif   "&char"<>"}",.error
&parm          amid  &parm,2,l:&parm-2
               ldy   #2
               lda   (&parm),y
               pha
               lda   (&parm)
               pha
               ago   .shorten
.chk2
               aif   "&char"<>"[",.absolute
               ldy   #2
               lda   &parm,y
               pha
               lda   &parm
               pha
               ago   .shorten
.absolute
               lda   &parm+2
               pha
               lda   &parm
               pha
               ago   .shorten
.at
&char1         amid  &parm,2,1
&char2         setc  &char1
               ph&char1
               aif   l:&parm<3,.chk2a
&char2         amid  &parm,3,1
.chk2a
               ph&char2
               ago   .shorten

.immediate
&parm          amid  &parm,2,l:&parm-1
               pea   +(&parm)|-16
               pea   &parm
               ago   .done
.shorten
               aif   s:longa=1,.done
               sep   #%00100000
.done
               mexit
.error
               mnote "Missing closing '}'",16
               mend

***********************************************

               macro
&lab           pl2   &parm
               lclc  &char
&lab           anop
               aif   s:longa=1,.start
               rep   #%00100000
.start
&char          amid  &parm,1,1
               aif   "&char"="@",.at
               aif   "&char"<>"{",.absolute
&char          amid  &parm,l:&parm,1
               aif   "&char"<>"}",.error
&parm          amid  &parm,2,l:&parm-2
               pla
               sta   (&parm)
               ago   .shorten
.absolute
               pla
               sta   &parm
               ago   .shorten
.at
&char          amid  &parm,2,1
               pl&char
.shorten
               aif   s:longa=1,.done
               sep   #%00100000
.done
               mexit
.error
               mnote "Missing Closing '}'",16
               mend

********************************************

               macro
&lab           pl4   &parm
               lclc  &char
               lclc  &char1
               lclc  &char2
&lab           anop
               aif   s:longa=1,.start
               rep   #%00100000
.start
&char          amid  &parm,1,1
               aif   "&char"<>"{",.chk
&char          amid  &parm,l:&parm,1
               aif   "&char"<>"}",.error
&parm          amid  &parm,2,l:&parm-2
               pla
               sta   (&parm)
               ldy   #2
               pla
               sta   (&parm),y
               ago   .shorten
.chk
               aif   "&char"<>"[",.chk2
               pla
               sta   &parm
               ldy   #2
               pla
               sta   &parm,y
               ago   .shorten
.chk2
               aif   "&char"<>"@",.absolute
&char1         amid  &parm,2,1
&char2         setc  &char1
               pl&char1
               aif   l:&parm<3,.chk2a
&char2         amid  &parm,3,1
.chk2a
               pl&char2
               ago   .shorten
.absolute
               pla
               sta   &parm
               pla
               sta   &parm+2
.shorten
               aif   s:longa=1,.done
               sep   #%00100000
.done
               mexit
.error
               mnote "Missing closing '}'",16
               mend

*************************************

               macro
&lab           Str   &string
&lab           dc    i1'L:&string'
               dc    c"&string"
               mend

*************************************

               macro
&lab           Str1  &string
&lab           dc    i2'L:&string'
               dc    c"&string"
               mend

*************************************

               macro
&lab           GSStr &string
&lab           dc    i2'L:&string'
               dc    c"&string"
               mend

*************************************

               macro
&lab           LD2   &val,&adr
&lab           lcla  &count
               lda   #&val
&count         seta  1
.loop
               sta   &adr(&count)
&count         seta  &count+1
               aif   &count>c:&adr,.done
               ago   ^loop
.done
               mend

*************************************

               macro
&lab           LD4   &val,&adr
&lab           lcla  &count
               lda   #<&val
&count         seta  1
.loop1
               sta   &adr(&count)
&count         seta  &count+1
               aif   &count>c:&adr,.part2
               ago   ^loop1
.part2
               lda   #+(&val)|-16
&count         seta  1
.loop2
               sta   &adr(&count)+2
&count         seta  &count+1
               aif   &count>c:&adr,.done
               ago   ^loop2
.done
               mend

*************************************

               macro
&lab           MV2   &src,&adr
&lab           lcla  &count
               lda   &src
&count         seta  1
.loop
               sta   &adr(&count)
&count         seta  &count+1
               aif   &count>c:&adr,.done
               ago   ^loop
.done
               mend

*************************************

               macro
&lab           MV4   &src,&adr
&lab           lcla  &count
               lda   &src
&count         seta  1
.loop1
               sta   &adr(&count)
&count         seta  &count+1
               aif   &count>c:&adr,.part2
               ago   ^loop1
.part2
               lda   &src+2
&count         seta  1
.loop2
               sta   &adr(&count)+2
&count         seta  &count+1
               aif   &count>c:&adr,.done
               ago   ^loop2
.done
               mend

*************************************

               macro
&lab           proc  &rg
&lab           anop
               aif   c:&rg,.cc
               lclc  &rg
.cc
               aif   c:&reg,.dd
               gblc  &reg
.dd
&reg           setc  ""
               phb
               aif   l:&rg=0,.bb
&reg           setc  &rg
               lcla  &plen
               lclc  &ch
               lcla  &i
&plen          seta  l:&reg
&i             seta  0
.aa
               aif   &i=&plen,.bb
&i             seta  &i+1
&ch            amid  &reg,&i,1
               ph&ch
               ago   ^aa
.bb
               phk
               plb
               mend

**************************************

               macro
&lab           procend
&lab           anop
               lclc  &ch
               lcla  &plen
&plen          seta  l:&reg
.aa
               aif   &plen=0,.bb
&ch            amid  &reg,&plen,1
               pl&ch
&plen          seta  &plen-1
               ago   ^aa
.bb
               plb
               rts
               mend

**************************************

               macro
&lab           procendL
&lab           anop
               lclc  &ch
               lcla  &plen
&plen          seta  l:&reg
.aa
               aif   &plen=0,.bb
&ch            amid  &reg,&plen,1
               pl&ch
&plen          seta  &plen-1
               ago   ^aa
.bb
               plb
               rtl
               mend

***************************************

               macro
&lab           long  &stat
&lab           anop
               lcla  &t
               lcla  &len
               lclc  &ch
&t             seta  0
&len           seta  l:&stat
.a
               aif   &len=0,.b
&ch            amid  &stat,&len,1
               aif   ("&ch"="x").or.("&ch"="y").or.("&ch"="i"),.i
               aif   ("&ch"="a").or.("&ch"="m"),.m
.c
&len           seta  &len-1
               ago   ^a
.i
               longi on
&t             seta  &t+16
               ago   ^c
.m
               longa on
&t             seta  &t+32
               ago   ^c
.b
               aif   &t=0,.d
               rep   #&t
.d
               mend

***************************************

               macro
&lab           short &stat
&lab           anop
               lcla  &t
               lcla  &len
               lclc  &ch
&t             seta  0
&len           seta  l:&stat
.a
               aif   &len=0,.b
&ch            amid  &stat,&len,1
               aif   ("&ch"="x").or.("&ch"="y").or.("&ch"="i"),.i
               aif   ("&ch"="a").or.("&ch"="m"),.m
.c
&len           seta  &len-1
               ago   ^a
.i
               longi off
&t             seta  &t+16
               ago   ^c
.m
               longa off
&t             seta  &t+32
               ago   ^c
.b
               aif   &t=0,.d
               sep   #&t
.d
               mend

****************************************

               macro
&lab           native
&lab           clc
               xce
               mend

****************************************

               macro
&lab           emulate
&lab           sec
               xce
               longa off
               longi off
               mend

****************************************

               macro
&lab           lsr2  &a
&lab           lsr   &a
               lsr   &a
               mend

*****************************************

               macro
&lab           lsr3  &a
&lab           lsr   &a
               lsr   &a
               lsr   &a
               mend

*****************************************

               macro
&lab           lsr4  &a
&lab           lsr   &a
               lsr   &a
               lsr   &a
               lsr   &a
               mend

****************************************

               macro
&lab           asl2  &a
&lab           asl   &a
               asl   &a
               mend

*****************************************

               macro
&lab           asl3  &a
&lab           asl   &a
               asl   &a
               asl   &a
               mend

*****************************************

               macro
&lab           asl4  &a
&lab           asl   &a
               asl   &a
               asl   &a
               asl   &a
               mend

****************************************

               macro
&lab           inx2
&lab           inx
               inx
               mend

****************************************

               macro
&lab           inx4
&lab           inx
               inx
               inx
               inx
               mend

****************************************

               macro
&lab           iny2
&lab           iny
               iny
               mend

****************************************

               macro
&lab           iny4
&lab           iny
               iny
               iny
               iny
               mend

****************************************

               macro
&lab           inc2  &a
&lab           inc   &a
               inc   &a
               mend

****************************************

               macro
&lab           inc4  &a
&lab           inc   &a
               inc   &a
               inc   &a
               inc   &a
               mend

****************************************

               macro
&lab           dex2
&lab           dex
               dex
               mend

****************************************

               macro
&lab           dex4
&lab           dex
               dex
               dex
               dex
               mend

****************************************

               macro
&lab           dey2
&lab           dey
               dey
               mend

****************************************

               macro
&lab           dey4
&lab           dey
               dey
               dey
               dey
               mend

****************************************

               macro
&lab           dec2  &a
&lab           dec   &a
               dec   &a
               mend

****************************************

               macro
&lab           dec4  &a
&lab           dec   &a
               dec   &a
               dec   &a
               dec   &a
               mend

*****************************************

               macro
&lab           vblwait
&lab           lda   $E1C019-1
               bmi   *-4
               lda   $E1C019-1
               bpl   *-4
               mend

******************************************

               macro
&lab           add2  &arg1,&arg2,&dest
               lclc  &char
&lab           clc
&char          amid  &arg1,1,1
               aif   "&char"="@",.at1
               lda   &arg1
               ago   .add
.at1
&char          amid  &arg1,2,1
               aif   "&char"="x",.x1
               aif   "&char"="X",.x1
               aif   "&char"="y",.y1
               aif   "&char"="Y",.y1
               ago   .add
.x1
               txa
               ago   .add
.y1
               tya
.add
               adc   &arg2
&char          amid  &dest,1,1
               aif   "&char"="@",.at2
               sta   &dest
               ago   .b
.at2
&char          amid  &dest,2,1
               aif   "&char"="x",.x2
               aif   "&char"="X",.x2
               aif   "&char"="y",.y2
               aif   "&char"="Y",.y2
               ago   .b
.x2
               tax
               ago   .b
.y2
               tay
.b
               mend

******************************************

               macro
&lab           add4  &arg1,&arg2,&dest
&lab           anop
               lclc  &ch
&ch            amid  &arg2,1,1

               clc
               lda   &arg1
               adc   &arg2
               sta   &dest
               lda   &arg1+2
               aif   "&ch"="#",.a
               adc   &arg2+2
               ago   .b
.a
               adc   &arg2|-16
.b
               sta   &dest+2

               mend

******************************************

               macro
&lab           sadd2  &arg1,&arg2,&dest
&lab           anop
               lclc  &ch
&ch            amid  &arg2,1,1

               clc
               lda   &arg1
               adc   &arg2
               sta   &dest
               lda   &arg1+1
               aif   "&ch"="#",.a
               adc   &arg2+1
               ago   .b
.a
               adc   &arg2|-16
.b
               sta   &dest+1

               mend

******************************************

               macro
&lab           sub2  &arg1,&arg2,&dest
               lclc  &char
&lab           sec
&char          amid  &arg1,1,1
               aif   "&char"="@",.at1
               lda   &arg1
               ago   .sub
.at1
&char          amid  &arg1,2,1
               aif   "&char"="x",.x1
               aif   "&char"="X",.x1
               aif   "&char"="y",.y1
               aif   "&char"="Y",.y1
               ago   .sub
.x1
               txa
               ago   .sub
.y1
               tya
.sub
               sbc   &arg2
&char          amid  &dest,1,1
               aif   "&char"="@",.at2
               sta   &dest
               ago   .b
.at2
&char          amid  &dest,2,1
               aif   "&char"="x",.x2
               aif   "&char"="X",.x2
               aif   "&char"="y",.y2
               aif   "&char"="Y",.y2
               ago   .b
.x2
               tax
               ago   .b
.y2
               tay
.b
               mend

******************************************

               macro
&lab           sub4  &arg1,&arg2,&dest
&lab           anop
               lclc  &ch
&ch            amid  &arg2,1,1

               sec
               lda   &arg1
               sbc   &arg2
               sta   &dest
               lda   &arg1+2
               aif   "&ch"="#",.a
               sbc   &arg2+2
               ago   .b
.a
               sbc   &arg2|-16
.b
               sta   &dest+2

               mend

******************************************

               macro
&lab           ssub2  &arg1,&arg2,&dest
&lab           anop
               lclc  &ch
&ch            amid  &arg2,1,1

               sec
               lda   &arg1
               sbc   &arg2
               sta   &dest
               lda   &arg1+1
               aif   "&ch"="#",.a
               sbc   &arg2+1
               ago   .b
.a
               sbc   &arg2|-16
.b
               sta   &dest+1

               mend

*******************************************

               macro
&lab           sto2  &var,&mat,&idx
&lab           anop
               aif   "&idx"="@",.noidx
               ldy   &idx
.noidx
               lda   &var
               sta   [&mat],y
               mend

*******************************************

               macro
&lab           sto4  &var,&mat,&idx
&lab           anop
               aif   "&idx"="@",.noidx
               ldy   &idx
.noidx
               lda   &var
               sta   [&mat],y
               iny2
               lda   &var+2
               sta   [&mat],y
               mend

*******************************************

               macro
&lab           get2  &mat,&var,&idx
&lab           anop
               aif   "&idx"="@",.noidx
               ldy   &idx
.noidx
               lda   [&mat],y
               sta   &var
               mend

*******************************************

               macro
&lab           get4  &mat,&var,&idx
&lab           anop
               aif   "&idx"="@",.noidx
               ldy   &idx
.noidx
               lda   [&mat],y
               sta   &var
               iny2
               lda   [&mat],y
               sta   &var+2
               mend

******************************************

               macro
&lab           and2  &arg1,&arg2,&dest
&lab           anop
               lclc  &char
&char          amid  &arg1,1,1
               aif   "&char"="@",.at1
               lda   &arg1
               ago   .add
.at1
&char          amid  &arg1,2,1
               aif   "&char"="x",.x1
               aif   "&char"="X",.x1
               aif   "&char"="y",.y1
               aif   "&char"="Y",.y1
               ago   .add
.x1
               txa
               ago   .add
.y1
               tya
.add
               and   &arg2
&char          amid  &dest,1,1
               aif   "&char"="@",.at2
               sta   &dest
               ago   .b
.at2
&char          amid  &dest,2,1
               aif   "&char"="x",.x2
               aif   "&char"="X",.x2
               aif   "&char"="y",.y2
               aif   "&char"="Y",.y2
               ago   .b
.x2
               tax
               ago   .b
.y2
               tay
.b
               mend

******************************************

               macro
&lab           ora2  &arg1,&arg2,&dest
&lab           anop
               lclc  &char
&char          amid  &arg1,1,1
               aif   "&char"="@",.at1
               lda   &arg1
               ago   .add
.at1
&char          amid  &arg1,2,1
               aif   "&char"="x",.x1
               aif   "&char"="X",.x1
               aif   "&char"="y",.y1
               aif   "&char"="Y",.y1
               ago   .add
.x1
               txa
               ago   .add
.y1
               tya
.add
               ora   &arg2
&char          amid  &dest,1,1
               aif   "&char"="@",.at2
               sta   &dest
               ago   .b
.at2
&char          amid  &dest,2,1
               aif   "&char"="x",.x2
               aif   "&char"="X",.x2
               aif   "&char"="y",.y2
               aif   "&char"="Y",.y2
               ago   .b
.x2
               tax
               ago   .b
.y2
               tay
.b
               mend

******************************************

               macro
&lab           eor2  &arg1,&arg2,&dest
&lab           anop
               lclc  &char
&char          amid  &arg1,1,1
               aif   "&char"="@",.at1
               lda   &arg1
               ago   .add
.at1
&char          amid  &arg1,2,1
               aif   "&char"="x",.x1
               aif   "&char"="X",.x1
               aif   "&char"="y",.y1
               aif   "&char"="Y",.y1
               ago   .add
.x1
               txa
               ago   .add
.y1
               tya
.add
               eor   &arg2
&char          amid  &dest,1,1
               aif   "&char"="@",.at2
               sta   &dest
               ago   .b
.at2
&char          amid  &dest,2,1
               aif   "&char"="x",.x2
               aif   "&char"="X",.x2
               aif   "&char"="y",.y2
               aif   "&char"="Y",.y2
               ago   .b
.x2
               tax
               ago   .b
.y2
               tay
.b
               mend

******************************************

               macro
&lab           neg2
&lab           eor   #$FFFF
               inc   a
               mend

******************************************

               macro
&lab           if2   &var,&rel,&val,&label
&lab           ago   .skip
               ble                      ;force macro inclusion just
               bgt                      ; in case these are used.
.skip
               lclc  &char1
               lclc  &char2
&char1         amid  &var,1,1
&char2         amid  &var,2,1
               aif   "&char1"="@",.index
               lda   &var
.cmp
               cmp   &val
               ago   .branch
.index
               aif   "&char2"="x",.x1
               aif   "&char2"="X",.x1
               aif   "&char2"="y",.y1
               aif   "&char2"="Y",.y1
               ago   ^cmp
.x1
               cpx   &val
               ago   .branch
.y1
               cpy   &val

.branch
&char1         amid  &rel,1,1
               aif   "&char1"="@",.done
               b&rel &label
.done
               mend

***************************************

               macro
&lab           bgt   &loc
&lab           beq   *+4
               bcs   &loc
               mend

***************************************

               macro
&lab           ble   &loc
&lab           bcc   &loc
               beq   &loc
               mend

***************************************

               macro
&lab           jeq   &loc
&lab           bne   *+5
               jmp   &loc
               mend

***************************************

               macro
&lab           jne   &loc
&lab           beq   *+5
               jmp   &loc
               mend

***************************************

               macro
&lab           jmi   &loc
&lab           bpl   *+5
               jmp   &loc
               mend

***************************************

               macro
&lab           jpl   &loc
&lab           bmi   *+5
               jmp   &loc
               mend

***************************************

               macro
&lab           jcc   &loc
&lab           bcs   *+5
               jmp   &loc
               mend

***************************************

               macro
&lab           jcs   &loc
&lab           bcc   *+5
               jmp   &loc
               mend

***************************************

               macro
&lab           jge   &loc
&lab           bcc   *+5
               jmp   &loc
               mend

***************************************

               macro
&lab           jle   &loc
&lab           bcs   *+5
               jmp   &loc
               mend

***************************************

               macro
&lab           deref &arg1,&arg2
&lab           lda   [&arg1]
               sta   &arg2
               ldy   #2
               lda   [&arg1],y
               sta   &arg2+2
               mend
